plugins {
    id 'java'
    id 'maven'
    id 'com.gradle.plugin-publish' version '0.9.5'
    id 'net.researchgate.release' version '2.4.0'
}

group 'org.echocat.gradle.plugins'
description 'Gradle plugin to build, test and do other stuff of Golang projects.'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    // Commons
    compile group: 'com.google.code.findbugs', name: 'jsr305', version: '1.3.9'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.1'
    compile group: 'org.apache.commons', name: 'commons-io', version: '1.3.2'
    compile group: 'org.apache.commons', name: 'commons-compress', version: '1.10'
    compile group: 'org.codehaus.plexus', name: 'plexus-utils', version: '3.0.22'

    // REST
    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.3.1'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.6.2'

    // VCS
    compile group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '4.6.1.201703071140-r'

    // Platform
    compile gradleApi()

    // Testing
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

pluginBundle {
    website = 'https://github.com/echocat/gradle-golang-plugin'
    vcsUrl = 'https://github.com/echocat/gradle-golang-plugin'
    tags = ['golang', 'go', 'build', 'test', 'cover']
    description = project.description

    plugins {
        golangPlugin {
            id = 'org.echocat.golang'
            displayName = 'Golang Plugin'
        }
        golangMinorPlugin {
            id = 'org.echocat.golang-minor'
            displayName = 'Golang Minor Plugin'
        }
    }

    mavenCoordinates {
        groupId = project.group
        artifactId = project.name
        version = project.version
    }
}

processResources {
    filesMatching('**/version.properties') {
        expand(
                'version': "$project.version",
                'group': "$project.group",
                'description': "$project.description"
        )
    }
}

release {
    preTagCommitMessage = 'Preapre release of '
    tagCommitMessage = 'Release version '
    newVersionCommitMessage = 'Prepare next development version '
    tagTemplate = 'v${version}'
}

afterReleaseBuild.dependsOn publishPlugins

tasks.withType(JavaCompile) {
    options.warnings = false
    options.encoding = "UTF-8"
}
